#version 460
layout (local_size_x = 16, local_size_y = 16) in;

#define SIZEOF_NODE 12
#define NODE_WIDTH 4
#define NODE_WIDTH_SQRT 2
#define WORLD_WIDTH 1024
#define MAX_DEPTH 6

#define EPSILON 1e-6f
#define MINI_STEP_SIZE 5e-2f
#define MICRO_STEP_SIZE 2e-3f

struct Node {
    uint bitmask_low;
    uint bitmask_high;
    uint header;
};

struct VoxelPair {
    uint voxel_pair;
};

layout (rgba8, set = 0, binding = 0) uniform image2D img;
layout (set = 0, binding = 1) uniform CameraData {
    vec4 position;
    mat4 invView;
    mat4 invProj;
} camera;

layout (std430, set = 0, binding = 2) buffer Nodes {
    Node node_pool[];
};

layout (std430, set = 0, binding = 3) buffer Voxels {
    VoxelPair voxel_pool[];
};

/**
 * Basic axis-aligned bounding box collision check.
 */
float AABBIntersect(vec3 bmin, vec3 bmax, vec3 orig, vec3 invdir, out vec3 aabb_mask) {
    vec3 t0 = (bmin - orig) * invdir;
    vec3 t1 = (bmax - orig) * invdir;
    vec3 vmin = min(t0, t1), vmax = max(t0, t1);
    float tmin = max(vmin.x, max(vmin.y, vmin.z));
    float tmax = min(vmax.x, min(vmax.y, vmax.z));
    if (tmax < tmin || tmax < 0.0) return -1.0;
    aabb_mask = step(vmin, vec3(tmin));
    return max(0.0, tmin);
}
/**
 * Our raytracing function
 */
uint raytrace(inout vec3 ray_pos, vec3 ray_dir) {
    // caching a few commonly used values
    const vec3 inverted_ray_dir = 1.0f / ray_dir;
    const vec3 ray_sign_11 = vec3(ray_dir.x < 0. ? -1. : 1., ray_dir.y < 0. ? -1. : 1., ray_dir.z < 0. ? -1. : 1.);
    const vec3 ray_sign_01 = max(ray_sign_11, 0.);

    // a variable used to know which direction we have to mini-step to after each step (including the AABB jump step)
    vec3 step_mask;

    // ray-box intersection and a big step if the camera is outside the voxel volume
    const vec3 bmin = vec3(MINI_STEP_SIZE), bmax = vec3(WORLD_WIDTH - MINI_STEP_SIZE);
    if (any(greaterThanEqual(ray_pos, bmax)) || any(lessThan(ray_pos, bmin))) {
        const float intersect = AABBIntersect(bmin, bmax, camera.position.xyz, inverted_ray_dir, step_mask);
        if (intersect < 0) return 0xffffffffu; // not bothering with rays that will not hit the bbox
        if (intersect > 0) ray_pos += ray_dir * intersect + step_mask * ray_sign_11 * MINI_STEP_SIZE;
    }

    // creating variables to track the size and bounding box of the **current** node, as well as the state of the current step
    vec3 lbmin = vec3(0), lbmax = vec3(WORLD_WIDTH);
    uint node_width = uint(WORLD_WIDTH) >> NODE_WIDTH_SQRT;
    bool has_collided, exited_local = false, exited_global = false;

    // setting up the stack & recurrent variables that will be used to traverse the 64-tree
    uint stack[MAX_DEPTH];
    uint depth = 0;
    uint current_node_index = 0;
    uint bitmask_index;
    Node current_node = node_pool[current_node_index];
    stack[depth] = current_node_index;

    // step limit
    uint step_count = 0;
    uint max_step_count = 256;

    // Doing the actual traversal!
    do {
    /**
         * For the most part, we are doing the classical DDA algorithm in this do-while loop
         */
        do {
            // increment step count
            step_count += 1;
            if (step_count > max_step_count) return 0xffffffffu;

            // check hit
            uvec3 v = (uvec3(ray_pos) & (node_width * NODE_WIDTH - 1u)) >> findMSB(node_width);
            bitmask_index = v.x + (v.z << NODE_WIDTH_SQRT) + (v.y << NODE_WIDTH);
            has_collided = (((bitmask_index < 32) ? current_node.bitmask_low : current_node.bitmask_high) & (1u << (bitmask_index & 31))) != 0;
            if (has_collided) break;

            // dda step, normal extraction, and mini-step
            vec3 side_dist = inverted_ray_dir * (node_width * ray_sign_01 - mod(ray_pos, node_width));
            float ray_step = min(min(side_dist.x, side_dist.y), side_dist.z);
            step_mask = vec3(equal(ray_step.xxx, side_dist));
            ray_pos += ray_dir * ray_step + MINI_STEP_SIZE * ray_sign_11 * step_mask;

            // check bbox
            exited_local = any(greaterThanEqual(ray_pos, lbmax)) || any(lessThan(ray_pos, lbmin));
            exited_global = any(greaterThanEqual(ray_pos, bmax)) || any(lessThan(ray_pos, bmin));
        } while (!has_collided && !exited_local && !exited_global);
    /**
         * First possible reason for exiting the DDA main loop: we hit something, so we need to either go down or return a color
         */
        if (has_collided) {
            do {
                // we lookup the node index of the hit to know where to descend to
                uint filtered_low, filtered_high;
                if (bitmask_index < 32) { filtered_low = current_node.bitmask_low & ((1u << bitmask_index) - 1u); filtered_high = 0u; }
                else { filtered_low = current_node.bitmask_low; filtered_high = current_node.bitmask_high & ((1u << (bitmask_index - 32)) - 1u); }
                uint hit_index = uint(bitCount(filtered_low) + bitCount(filtered_high)) + uint((current_node.header & ~(0x1u << 31)));

                // if there is a hit on a voxel in a terminal node: return hit color
                if ((current_node.header & (0x1u << 31)) != 0) {
                    ray_pos -= vec3(MINI_STEP_SIZE) * ray_sign_11;
                    return hit_index;
                }

                // going down
                depth += 1;
                stack[depth] = current_node_index;
                current_node_index = hit_index;
                current_node = node_pool[hit_index];
                node_width = node_width >> NODE_WIDTH_SQRT;

                // check hit
                uvec3 v = (uvec3(ray_pos) & (node_width * NODE_WIDTH - 1u)) >> findMSB(node_width);
                bitmask_index = v.x + (v.z << NODE_WIDTH_SQRT) + (v.y << NODE_WIDTH);
                uint mask = 1u << (bitmask_index & 31);
                has_collided = ((bitmask_index < 32) ? (current_node.bitmask_low & mask) : (current_node.bitmask_high & mask)) != 0;

                // quick check to prevent accidents
                if (depth > MAX_DEPTH)  return 0xffffffffu;
            } while (has_collided);

            // update lbb
            lbmin = uvec3(ray_pos) & uvec3(~(node_width * NODE_WIDTH - 1u));
            lbmax = lbmin + uvec3(node_width * NODE_WIDTH);
        }

        /**
         * Second possible reason for exiting the DDA main loop: we exited the current node, we have to go up
         */
        else if (exited_local && !exited_global) {
            do {
                // quick check to prevent accidents
                if (depth == 0) return 0xffffffffu;

                // go up
                current_node_index = stack[depth];
                current_node = node_pool[current_node_index];
                depth -= 1;

                // update node width
                node_width = node_width << NODE_WIDTH_SQRT;

                // update lbb
                lbmin = uvec3(lbmin) & uvec3(~(node_width * NODE_WIDTH - 1u));
                lbmax = lbmin + uvec3(node_width * NODE_WIDTH);

                // check if we're good to resume the DDA
                exited_local = any(greaterThanEqual(ray_pos, lbmax)) || any(lessThan(ray_pos, lbmin));
            } while (exited_local);
        }
    } while (!exited_global);
    return 0xffffffffu;
} // fix micro-step out of bound

/**
 * Doing some maths to get the ray dir given the camera position, direction, and the target pixel on-screen coordinates
 */
vec3 get_ray_dir(ivec2 screen_position) {
    ivec2 screen_size = imageSize(img);
    vec2 screen_space = ((screen_position + vec2(0.5)) / screen_size).xy;
    screen_space.y = 1.0 - screen_space.y;
    vec4 clip_space = vec4(screen_space * 2.0f - 1.0f, -1.0, 1.0);
    vec4 eye_space = vec4(vec2(camera.invProj * clip_space), -1.0, 0.0);
    vec3 ray_dir = normalize(vec3(camera.invView * eye_space));
    return ray_dir;
    vec3 is_small = step(abs(ray_dir), vec3(EPSILON)); // 1.0 if |dir| < EPSILON
    return ray_dir + is_small * EPSILON * sign(ray_dir + 0.5); // nudge
}

/**
 * Our voxel color table
 */
const vec3 colors[] = {
vec3(0.69, 0.88, 0.90), // SKY_BLUE
vec3(1.00, 0.40, 0.40), // DEBUG_RED
vec3(0.40, 1.00, 0.40), // DEBUG_GREEN
vec3(0.40, 0.40, 1.00), // DEBUG_BLUE
vec3(0.55, 0.55, 0.55), // STONE
vec3(0.42, 0.32, 0.25), // DIRT
vec3(0.30, 0.59, 0.31)  // GRASS
};

/**
 * Main function!
 */
void main() {
    // make sure current thread is inside the window bounds
    ivec2 screen_size = imageSize(img);
    if (any(greaterThanEqual(gl_GlobalInvocationID.xy, screen_size))) return;

    // get the ray direction for the current pixel
    const vec3 ray_dir = get_ray_dir(ivec2(gl_GlobalInvocationID.xy));

    // primary ray
    vec3 ray_pos = camera.position.xyz;
    uint voxel_index = raytrace(ray_pos, ray_dir);
    if (voxel_index == 0xffffffffu) {imageStore(img, ivec2(gl_GlobalInvocationID.xy), vec4(colors[0], 0)); return;}
    uint pair_index = voxel_index >> 1;
    uint voxel_pair = voxel_pool[pair_index].voxel_pair;
    uint voxel = (voxel_index & 0x1u) != 0 ? (voxel_pair >> 16) & 0xFFFFu : (voxel_pair & 0xFFFFu);
    uint material = voxel & 0x7fu;
    uint encoded_normal = (voxel >> 7);
    vec3 normal = normalize(vec3(float((encoded_normal >> 6) & 0x7u), float((encoded_normal >> 3) & 0x7u), float(encoded_normal & 0x7u)) / 3.5 - 1.0);
    vec3 voxel_color = colors[material < 7 ? material : 1].xyz;

    // sun direction
    float angle = camera.position.w * 1.0;
    float c = cos(angle);
    float s = sin(angle);
    const vec3 base_sun_direction = normalize(vec3(0.4, 0.4, 1.0));
    vec3 sun_direction = normalize(vec3(
                                   c * base_sun_direction.x - s * base_sun_direction.y,
                                   s * base_sun_direction.x + c * base_sun_direction.y,
                                   base_sun_direction.z
                                   ));

    // diffuse shading
    float diffuse = 0.5 + dot(normalize(normal), normalize(sun_direction.xyz)) / 2;
    if (diffuse < 0.5) { diffuse = diffuse / 3 + 0.3333;};

    // secondary ray
    float shadow = 1.0;
    ray_pos = ray_pos + normal.xzy * 1.0;
    uint sun_voxel_index = raytrace(ray_pos, sun_direction.xzy);
    if (sun_voxel_index != 0xffffffffu) shadow = 0.5; // Reduced brightness for shadows

    // V1: I want
    // I want 0.5 in shadows
    // I want 1.0 when colinear, and 0.5 when not not colinear

    // writing to the framebuffer
    imageStore(img, ivec2(gl_GlobalInvocationID.xy), vec4(voxel_color * min(diffuse, shadow), 0));
}